/**
 * File:
 *  fcoe-client_finish.ycp
 *
 * Module:
 *  Step of base installation finish
 *
 * Authors:
 *  Gabriele Mohr <gs@suse.de>
 *
 */

{

textdomain "fcoe-client";

import "Directory";
import "String";
import "FcoeClient";
import "Service";
include "installation/misc.ycp";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string)WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map)WFM::Args(1);
}

y2milestone ("starting fcoe-client_finish");
y2debug("func=%1", func);
y2debug("param=%1", param);

if (func == "Info")
{
    return (any)$[
	"steps" : 1,
	// progress step title
	"title" : _("Saving fcoe configuration..."),
	"when" : [ `installation, `update, `autoinst ],
    ];
}
else if (func == "Write")
{
    boolean start_services = false;
    string command = "";
    list <map> netcards = FcoeClient::GetNetworkCards();

    if ( netcards != $[] )
    {
	y2milestone( "Copying files /etc/fcoe/* to destination" );
	// copy fcoe config files to destdir
	WFM::Execute (.local.bash,"test -d /etc/fcoe/ && mkdir -p '" + String::Quote(Installation::destdir) + "/etc/fcoe' && cp -a /etc/fcoe/* '" + String::Quote(Installation::destdir) + "/etc/fcoe/'");
    }
    else
    {
	y2milestone( "Nothing to do" );
    }

    foreach ( map card, netcards, {
	    string command = "";
	    string file_name = "";
	    if ( card["fcoe_vlan"]:"" != FcoeClient::NOT_AVAILABLE &&
		 card["fcoe_vlan"]:"" != FcoeClient::NOT_CONFIGURED )
	    {
		// FCoE VLAN interface is configured -> start services
		start_services = true;
		
		// copy sysconfig files
		file_name = sformat( "/etc/sysconfig/network/ifcfg-%1\.%2",
				     card["dev_name"]:"", card["vlan_interface"] );
		command = sformat ( "cp -a %1 '%2/etc/sysconfig/network'", file_name,
				    String::Quote(Installation::destdir) );
		y2milestone( "Executing command: %1", command );
		WFM::Execute (.local.bash, command );

		file_name = sformat( "/etc/sysconfig/network/ifcfg-%1", card["dev_name"]:"" );
		command = sformat ( "cp -a %1 '%2/etc/sysconfig/network'", file_name,
				    String::Quote(Installation::destdir) );
		y2milestone( "Executing command: %1", command );
		WFM::Execute (.local.bash, command );	
	    }
	} );

    if ( start_services )
    {
	y2milestone( "Enabling service start of fcoe and lldpad" ); 
	Service::Enable("boot.fcoe");
	Service::Enable("boot.lldpad"); 
    }
}
else
{
    y2error ("unknown function: %1", func);
    ret = nil;
}

y2debug("ret=%1", ret);
y2milestone("fcoe-client_finish finished");
return ret;


} /* EOF */
