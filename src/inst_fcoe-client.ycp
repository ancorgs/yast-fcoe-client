/**
 * File:	clients/inst_fcoe-client.ycp
 * Package:	Configuration of fcoe-client
 * Summary:	Main file
 * Authors:	Gabriele Mohr <gs@suse.de> 
 *
 *
 * File called in installation workflow for fcoe-client configuration.
 */

{

/***
 * <h3>Configuration of fcoe-client</h3>
 */

textdomain "fcoe-client";

/* The main () */
y2milestone ("----------------------------------------");
y2milestone ("FCoEClient module started");

import "Progress";
import "Report";
import "Summary";
import "ModuleLoading";
import "PackagesProposal";
import "Installation";
import "String";
import "FcoeClient";
import "NetworkService";

include "fcoe-client/wizards.ycp";

/* main ui function */
any ret = nil;
boolean success = false;

y2milestone("fcoe-client module started during installation");

// create /etc/fcoe
SCR::Execute(.target.bash, "mkdir -p /etc/fcoe" );

// TODO: check what is additional needed 
//       - load any modules ?
//	  
// FcoeClient::CheckInstalledPackages() not needed in inst-sys
// FcoeClient::DetectStartStatus()	doesn't make sense in inst-sys

// check for running network
if( !NetworkService::RunningNetworkPopup() )
{
    y2error( "Network NOT set up" );
    return nil;
}

// reset global values
FcoeClient::ResetNetworkCards();

// start services fcoe and lldpad
success = FcoeClient::ServiceStatus();
if ( !success )
{
    y2error( "Starting of services FAILED" );
    return nil;
}

// detect netcards
success = FcoeClient::DetectNetworkCards(); 
if ( !success )
{
    y2error( "Detecting netcards FAILED" );
    return nil;
}

// read general FCoE settings
success = FcoeClient::ReadFcoeConfig();
if ( !success )
{
    y2error( "Reading /etc/fcoe/config FAILED" );
    return nil;
}

// run dialog
ret = MainSequence();
y2milestone("MainSequence ret=%1", ret);

// workflow not aborted
if ( ret == `next )
{
    // add package open-fcoe to the pool that is used by software proposal
    y2milestone( "Adding package open-fcoe to pool" );
    PackagesProposal::AddResolvables( "fcoe", `package, ["open-fcoe"] ); 
    // write changes to config files
    y2milestone( "Writing config files");
    FcoeClient::WriteFcoeConfig();
    FcoeClient::WriteCfgFiles();
    // restart fcoemon
    y2milestone( "Restarting FCoE" );
    FcoeClient::RestartServiceFcoe();
    // enable start of services
    y2milestone( "Enabling service start of fcoe and lldpad" );
    FcoeClient::SetStartService( "fcoe", true );
    FcoeClient::SetStartService( "lldpad", true );
    FcoeClient::AdjustStartStatus();
    // reset modified flag
    FcoeClient::SetModified( false );
}

/* Finish */
y2milestone("fcoe-client module finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
