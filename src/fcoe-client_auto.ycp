/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	clients/fcoe-client_auto.ycp
 * Package:	Configuration of fcoe-client
 * Summary:	Client for autoinstallation
 * Authors:	Gabriele Mohr <gs@suse.de>
 *
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of fcoe-client settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("fcoe-client_auto", [ "Summary", mm ]);
 */

{

textdomain "fcoe-client";

y2milestone("----------------------------------------");
y2milestone("FcoeClient auto started");

import "FcoeClient";
include "fcoe-client/wizards.ycp";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string)WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map) WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Create a summary*/
if(func == "Summary") {
    ret = select(FcoeClient::Summary(), 0, "");
}
/* Reset configuration */
else if (func == "Reset") {
    FcoeClient::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    ret = FcoeClientAutoSequence();
}
/* Import configuration */
else if (func == "Import") {
    ret = FcoeClient::Import(param);
}
/* Return actual state */
else if (func == "Export") {
    ret = FcoeClient::Export();
}
/* Return needed packages */
else if (func == "Packages") {
    ret = FcoeClient::AutoPackages();
}
/* Read current state */
else if (func == "Read") {
    import "Progress";
    boolean progress_orig = Progress::set (false);
    ret = FcoeClient::Read();
    Progress::set (progress_orig);
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    boolean write = true;
    boolean progress_orig = Progress::set (false);
    FcoeClient::SetWriteOnly (true);
    // prepare for AutoYaST
    list<map> netcards = FcoeClient::GetNetworkCards();
    foreach ( map card, netcards, {
        string vlan_interface = "";
        string fcoe_vlan_interface = "";
        string command = "";
        map output = $[];

        if ( card["fcoe_vlan"]:"" != FcoeClient::NOT_AVAILABLE &&
             card["fcoe_vlan"]:"" != FcoeClient::NOT_CONFIGURED )
        {
            // create and start FcoE 
            command = sformat( "fipvlan -c -s %1", card["dev_name"]:"" );
            output = (map)SCR::Execute( .target.bash_output, command );
            y2milestone( "Output: %1", output );
            if ( output["exit"]:255 != 0 )
            {
                y2error( "Cannot create and start FCoE on %1", card["dev_name"]:"" );
		write = false;	// don't write configuration
            }
            else
            {
                // check FCoE VLAN interface
                vlan_interface = FcoeClient::GetVlanInterface( card["dev_name"]:"" );
                fcoe_vlan_interface = FcoeClient::GetFcoeVlanInterface( card["dev_name"]:"", 
                                                                        vlan_interface );
                if ( fcoe_vlan_interface != card["fcoe_vlan"]:"" )
                {
                    y2error( "FCoE VLAN interface not correct - created: %1, imported: %2",
                              fcoe_vlan_interface, card["fcoe_vlan"]:"" );
		    write = false;	// don't write configuration
                }
            }
        }
    });
    if ( write )
    {
	ret = FcoeClient::Write();
    }
    else	// don't continue in case of error
    {
	ret = false;
    }
    Progress::set (progress_orig);
}
/**
 * did configuration changed
 * return boolean
 */
else if (func == "GetModified") {
    ret = FcoeClient::Modified ();
}
/**
 * set configuration as changed
 * return boolean
 */
else if (func == "SetModified") {
    FcoeClient::SetModified (true);
    ret = true;
}

/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("FcoeClient auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
